# =========================================================
#-- GB1 08/03/2022 ----------------------------------------
# Call ENVOI_MAIL_SYRA() or ENVOI_MAIL()
# With parameter Theme
# and use the new package mailer Syracuse
# Must have sage-smtp  server mail
#==========================================================
Call OUVRE_TRACE("QLFAR_SEND_EMAIL_SYRA") From LECFIC
Call TESTSUITE()
Call FERME_TRACE From LECFIC
GTRACE = "QLFAR_SEND_EMAIL_SYRA_"+GACTX.USER
Call LEC_TRACE From LECFIC
End
# =======================================================================
#**
#* The main entry point of the unit test suite for table commands
#*
#*!
Funprog TESTSUITE()
#--- wait for Cathy push on integration ----
#--  If not :  Wrong parameter -------------
Call ADD_TESTCASE("SEND_EMAIL_SYRA","ENVOI_MAIL_SYRA()",2) From AXUNIT
Call ADD_TESTCASE("SEND_MELADX","ENVOI_MAIL()",2) From AXUNIT
# ================================================================
Local Clbfile  SUITE_RESULT
SUITE_RESULT =func AXUNIT.RUN_TESTSUITE("Send email from X3", "")
End SUITE_RESULT
# ==================================================================
# ==================================================================
Subprog SEND_EMAIL_SYRA()
Local Integer WTHEME
WTHEME = [V]CST_AYES #-- function with new parameter theme: 2-YES, 1-NO
# Call ENVOI_MAIL_SYRA()
Call SENDER_WITH(2,WTHEME)
Call CHECK_EQUAL("ENVOI_MAIL_SYRA","ENVOI_MAIL_SYRA") From AXUNIT
End
# ========================================
Subprog SEND_MELADX()
Local Integer WTHEME
WTHEME = [V]CST_AYES #-- function with new parameter theme: 2-YES, 1-NO
# Call ENVOI_MAIL()
Call SENDER_WITH(1,WTHEME)
Call CHECK_EQUAL("ENVOI_MAIL","ENVOI_MAIL") From AXUNIT
End
# ========================================
Funprog GET_WORKFLOW_EMAIL(WUSER)
Value Char WUSER
# -------------------------------
Local Char WEMAIL(40)
Local Instance  WCLASS Using C_AAUTILIS
WEMAIL=""
WCLASS = NewInstance C_AAUTILIS AllocGroup null
If (fmet WCLASS.AREAD(WUSER)=0)
WEMAIL=WCLASS.ADDEML
Endif
#-- Free instance ----
FreeGroup WCLASS
End WEMAIL
# ========================================
#**
#*  PTYPE     1=Call ENVOI_MAIL()/ 2=Call ENVOI_MAIL_SYRA()
#*  WTHEME    2=for new parameter theme / 1=old function without parameter theme
#*!
Subprog SENDER_WITH(PTYPE,WTHEME)
Value Integer PTYPE
Value Integer WTHEME # Yes (2) with the new param THEME at the end
# ----------------------------------------------------
Local Char WEXPMAIL(40)   :
Local Char WDESTMAIL(40)  :
Local Char CC0(40)        :
Local Char CC1(40)        :
Local Integer WATTACH     :
Local Char WFILENAME(250)(1..2)
#---- txt file --------------------------
Raz WFILENAME
WEXPMAIL = func GET_WORKFLOW_EMAIL(GUSER)
Call CHECK_NOTEQUAL(WEXPMAIL,"") From AXUNIT
WDESTMAIL=WEXPMAIL
CC0=WEXPMAIL : CC1=""
WATTACH=2 : # attachment 2 files
WFILENAME(1)= filpath("TRA","QLFAR_RC_TSKGRPSTATUS2_"+GUSER,"tra")
WFILENAME(2)= filpath("TRA","QLFAR_RC_TSKGRPSTATUS_"+GUSER,"tra")
#--- 2 : Call Syracuse ENVOI_MAIL_SYRA(),  1= Call ENVOI_MAIL()
Call X3_SEND_EMAIL(PTYPE,WTHEME,WEXPMAIL,WDESTMAIL,CC0,CC1,WATTACH,WFILENAME)
End
# ===============================================================
#- New - with the parameter THEME
# ===============================================================
#** 18/02/2022
#* @param WSYRA      :  2=ENVOI_MAIL_SYRA() 1=ENVOI_MAIL()
#* @param WEXPMAIL   :  Email From
#* @param WDESTMAIL  :  One Email To
#* @param CC0        :  first Cc / can be empty
#* @param CC1        :  second Cc / can be empty
#* @param WPJOI      :  1 Attach yes / 0 No
#* @param WFILENAME  :  File name if WPJOI > 0
#*
#*!
Subprog X3_SEND_EMAIL(WSYRA,WTHEME,WEXPMAIL,WDESTMAIL,CC0,CC1,WPJOI,WFILENAME)
Value Integer WSYRA      : # 2=ENVOI_MAIL_SYRA() 1=ENVOI_MAIL()
Value Integer WTHEME     : # 2 with new param Theme / 1 old function without param theme
Value Char    WEXPMAIL   : # From
Value Char    WDESTMAIL  : # send To
Value Char    CC0        : # send first Cc
Value Char    CC1        : # send second Cc
Value Integer WPJOI      : # piece joint >0 oui, 0 non
Value Char    WFILENAME()(1..)
# -------------------------------------------------
Local Char      L_EMAIL(60)(1..4), L_PCE(250)(1..2)
Local Char      L_TEXTE(250)(1..3) , L_OBJET(250)
Local Char      L_EXPMAIL(250)
Local Integer   L_ENVOI(1..4) , L_SUIVI(1..1)

Local Integer   USR_NB, K
Local Clbfile   TEXCLB(5)
Local Integer   WATTACH
Local Char      WDESKTOP(30), WMESS(80)
Local Char      WMOBILE(30), USR_CODE(80)(1..1)

Raz L_TEXTE
Raz L_EXPMAIL,L_EMAIL
#--- nothing to send -----------------
If (WEXPMAIL="" or WDESTMAIL="") : End
Endif
# ===========================
USR_NB=4 :
L_ENVOI(1) = 2 : # value 2 for send TO
L_ENVOI(2) = 2 : # value 2 for send TO
#---- Cc ---------------------
If (CC0 <> "")
L_ENVOI(3)= 3  : # value 3 for Cc
L_EMAIL(3)=CC0 : #
Else
L_ENVOI(3) = 0 : # no CC
USR_NB -= 1
Endif
If (CC1 <> "")
L_ENVOI(4) = 3 : # value 3 for Cc
L_EMAIL(4)=CC1 : #
Else
L_ENVOI(4) = 0 : # no CC
USR_NB -= 1
Endif
# ============================================
If (WSYRA=[V]CST_AYES)
L_TEXTE(1)="method ENVOI_MAIL_SYRA() " :
Endif
If (WSYRA=[V]CST_ANO)
L_TEXTE(1)="method ENVOI_MAIL() " :
Endif
L_TEXTE(1) += mess(85,115,1)-":"-nomap
L_TEXTE(2) -= mess(175,124,1)-":"-GUSER
L_TEXTE(3) -= mess(12,7710,1)-format$("D:"+GFMDAT,date$)-time$
L_EXPMAIL=WEXPMAIL : # expediteur
#---- Destinataires -----------------
L_EMAIL(1)=WDESTMAIL :
L_EMAIL(2)=WEXPMAIL  : # To sender also
# -----------------------------------
WATTACH = WPJOI : # pas d'attach 0 / >0 attach
If (WATTACH > 0)
Local Integer WOK
WMESS= "[With Attachment file]"
For K=1 To 2
L_PCE(K) = WFILENAME(K)
Call EXISTE (L_PCE(K),WOK) From ORDSYS
If (WOK=0)
WMESS="File "+L_PCE(K)+" Not found!"
WATTACH=0
Endif
Next K
Else
WMESS= "[Without Attachment]"
Raz L_PCE
Endif
L_OBJET = "Send - "+WMESS
Setlob TEXCLB With L_TEXTE(1..3)
If (WSYRA = [V]CST_ANO)
Call ENVOI_MAIL(2,1,0,GUSER,L_EXPMAIL,"","",num$(adxuid(1)),0
&                 ,L_EMAIL,L_ENVOI,L_SUIVI,USR_NB,
&                 TEXCLB ,L_OBJET,L_PCE,
&                 WATTACH,"",0,"",WTHEME,"") From AWRKMEL
Else
If (WSYRA = [V]CST_AYES)   : #-- by Syracuse
Raz USR_CODE  : #-- What is USR_CODE ?
# Use Syracuse -----
Call ENVOI_MAIL_SYRA(2,1,0,GUSER,L_EXPMAIL,"","",num$(adxuid(1)),0
&                 ,L_EMAIL,USR_CODE,L_ENVOI,L_SUIVI,USR_NB,
&                 TEXCLB ,L_OBJET,L_PCE,
&                 WATTACH,"",0,"",WDESKTOP,WMOBILE,WTHEME,"") From AWRKMEL
Endif  : # endif WSYRA
Endif
End
# =======================================================================
Funprog IGNORE()
# This script is not intended to be run independently as part of the daily test plan.
End [V]CST_ATRUE
